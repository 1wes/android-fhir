plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}
group = 'com.google.android.fhir'
version = "0.1.0-alpha01"
task sourceJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                artifactId = 'data-capture'
                // Also publish source code for developers' convenience
                artifact(sourceJar)

                pom {
                    name = 'Android FHIR Structured Data Capture Library'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                }
            }
        }
    }
}

android {
    compileSdkVersion versions.Sdk.compileSdk
    buildToolsVersion versions.Plugins.buildTools

    defaultConfig {
        minSdkVersion versions.Sdk.minSdk
        targetSdkVersion versions.Sdk.targetSdk
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // Need to specify this to prevent junit runner from going deep into our dependencies
        testInstrumentationRunnerArguments = ['package': 'com.google.android.fhir.datacapture']
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
        }
    }
    compileOptions {
        // Flag to enable support for the new language APIs
        // See https://developer.android.com/studio/write/java8-support
        coreLibraryDesugaringEnabled true
        // Sets Java compatibility to Java 8
        // See https://developer.android.com/studio/write/java8-support
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        // See https://developer.android.com/studio/write/java8-support
        jvmTarget = '1.8'
    }
}

configurations {
    all {
        exclude module: 'xpp3'
    }
}

dependencies {

    androidTestImplementation deps.TestDependencies.CoreTestDeps.core
    androidTestImplementation deps.TestDependencies.CoreTestDeps.ext_junit
    androidTestImplementation deps.TestDependencies.CoreTestDeps.ext_junit_ktx
    androidTestImplementation deps.TestDependencies.CoreTestDeps.runner
    androidTestImplementation deps.TestDependencies.truth

//    androidTestImplementation deps.atsl.ext_junit_ktx
//    androidTestImplementation deps.atsl.rules
//    androidTestImplementation deps.atsl.runner
//    androidTestImplementation deps.truth

    api deps.hapi_r4

    coreLibraryDesugaring deps.AppDependencies.CoreDeps.desugar
//    coreLibraryDesugaring deps.desugar

    implementation deps.AppDependencies.CoreDeps.appCompat
    implementation deps.AppDependencies.CoreDeps.core
    implementation deps.AppDependencies.CoreDeps.fragment
    implementation deps.AppDependencies.Kotlin.kotlin

    implementation deps.appcompat
    implementation deps.core
    implementation deps.fragment
    implementation deps.kotlin.stdlib
    implementation deps.kotlin.test
    implementation deps.lifecycle.viewmodel_ktx
    implementation deps.material

    testImplementation deps.atsl.core
    testImplementation deps.junit
    testImplementation deps.robolectric
    testImplementation deps.truth
}